/*

The MIT License (MIT)

Copyright (c) 2017-2022 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/


// Roe averaged Riemann solver
void upwind(const dfloat nx,
            const dfloat ny,
            const dfloat nz,
	    const dfloat4 iweights,
            const dfloat dHx,
            const dfloat dHy,
            const dfloat dHz,
            const dfloat dEx,
            const dfloat dEy,
            const dfloat dEz,
	    dfloat *fluxHx,
            dfloat *fluxHy,
            dfloat *fluxHz,
	    dfloat *fluxEx,
            dfloat *fluxEy,
            dfloat *fluxEz){

  dfloat alpha=1; // => full upwinding
  
  dfloat ndotdH = nx*dHx + ny*dHy + nz*dHz;
  dfloat ndotdE = nx*dEx + ny*dEy + nz*dEz;

  dfloat centHw = iweights.x;
  dfloat stabHw = iweights.y;
  dfloat centEw = iweights.z;
  dfloat stabEw = iweights.w;
  
  *fluxHx = centHw*(-ny*dEz + nz*dEy) + alpha*stabHw*(dHx - ndotdH*nx);
  *fluxHy = centHw*(-nz*dEx + nx*dEz) + alpha*stabHw*(dHy - ndotdH*ny);
  *fluxHz = centHw*(-nx*dEy + ny*dEx) + alpha*stabHw*(dHz - ndotdH*nz);
     
  *fluxEx = centEw*( ny*dHz - nz*dHy) + alpha*stabEw*(dEx - ndotdE*nx);
  *fluxEy = centEw*( nz*dHx - nx*dHz) + alpha*stabEw*(dEy - ndotdE*ny);
  *fluxEz = centEw*( nx*dHy - ny*dHx) + alpha*stabEw*(dEz - ndotdE*nz);
}
 
// batch process elements
@kernel void maxwellHeterogeneousSurfaceTet3D(const dlong Nelements,
					      @restrict const  dlong  *  elementIds,
					      @restrict const  dfloat *  sgeo,
					      @restrict const  dfloat *  LIFT,
					      @restrict const  dlong  *  vmapM,
					      @restrict const  dlong  *  vmapP,
					      @restrict const  int    *  EToB,
					      @restrict const  dfloat *  x,
					      @restrict const  dfloat *  y,
					      @restrict const  dfloat *  z,
					      @restrict const  dfloat *  intInterp, // interpolate to integration nodes
					      @restrict const  dfloat *  intLIFT, // lift from integration to interpolation nodes
					      @restrict const  dfloat4 *  intUpwindCoeffs, // flux term coefficients
					      @restrict const  dfloat *  mu,
					      const dfloat time,					      
					      @restrict const  dfloat *  q,
					      @restrict dfloat *  rhsq){

  // for all elements
  for(dlong eo=0;eo<Nelements;eo+=p_NblockS;@outer(0)){

    // reduces shared storage by processing one face at a time

    @shared dfloat s_dHx[p_NblockS][p_Nfp];
    @shared dfloat s_dHy[p_NblockS][p_Nfp];
    @shared dfloat s_dHz[p_NblockS][p_Nfp];
    @shared dfloat s_dEx[p_NblockS][p_Nfp];
    @shared dfloat s_dEy[p_NblockS][p_Nfp];
    @shared dfloat s_dEz[p_NblockS][p_Nfp];
    
    @shared dfloat s_ifluxHx[p_NblockS][p_intNfp];
    @shared dfloat s_ifluxHy[p_NblockS][p_intNfp];
    @shared dfloat s_ifluxHz[p_NblockS][p_intNfp];
    @shared dfloat s_ifluxEx[p_NblockS][p_intNfp];
    @shared dfloat s_ifluxEy[p_NblockS][p_intNfp];
    @shared dfloat s_ifluxEz[p_NblockS][p_intNfp];

    @exclusive dlong r_e, element;

    @exclusive dfloat LfluxHx, LfluxHy, LfluxHz, LfluxEx, LfluxEy, LfluxEz;

    for(int es=0;es<p_NblockS;++es;@inner(1)){
      for(int n=0;n<p_cubMaxNodes1;++n;@inner(0)){
	LfluxHx = 0; LfluxHy = 0; LfluxHz = 0;
	LfluxEx = 0; LfluxEy = 0; LfluxEz = 0;

	r_e = eo + es;
	element = (r_e<Nelements) ? elementIds[r_e]:-1;
      }
    }
    
#pragma unroll p_Nfaces
    for(int face=0;face<p_Nfaces;++face){

      @barrier();
      
      // for nodes on face 
      for(int es=0;es<p_NblockS;++es;@inner(1)){
	for(int n=0;n<p_cubMaxNodes1;++n;@inner(0)){ // cubMaxNodes1 = max(intNfp,Np)
	  
	  if(r_e<Nelements){
	    
	    if(n<p_Nfp){
	      // load surface geofactors for this face
	      const dlong sid    = p_Nsgeo*(element*p_Nfaces+face);
	      const dfloat nx   = sgeo[sid+p_NXID];
	      const dfloat ny   = sgeo[sid+p_NYID];
	      const dfloat nz   = sgeo[sid+p_NZID];
	      
	      // indices of negative and positive traces of face node
	      const dlong id  = element*p_Nfp*p_Nfaces + n + face*p_Nfp;
	      const dlong idM = vmapM[id];
	      const dlong idP = vmapP[id];

	      // load traces
	      const dlong eM = element;
	      const dlong eP = idP/p_Np;
	      const int vidM = idM%p_Np;
	      const int vidP = idP%p_Np;
	      
	      const dlong qbaseM = eM*p_Np*p_Nfields + vidM;
	      const dlong qbaseP = eP*p_Np*p_Nfields + vidP;
	      
	      const dfloat HxM = q[qbaseM + 0*p_Np];
	      const dfloat HyM = q[qbaseM + 1*p_Np];
	      const dfloat HzM = q[qbaseM + 2*p_Np];
	      const dfloat ExM = q[qbaseM + 3*p_Np];
	      const dfloat EyM = q[qbaseM + 4*p_Np];
	      const dfloat EzM = q[qbaseM + 5*p_Np];
	      
	      dfloat HxP = q[qbaseP + 0*p_Np];
	      dfloat HyP = q[qbaseP + 1*p_Np];
	      dfloat HzP = q[qbaseP + 2*p_Np];
	      dfloat ExP = q[qbaseP + 3*p_Np];
	      dfloat EyP = q[qbaseP + 4*p_Np];
	      dfloat EzP = q[qbaseP + 5*p_Np];
	      
	      // apply boundary condition
	      const int bc = EToB[face+p_Nfaces*element];
	      if(bc>0){
		maxwellDirichletConditions3D(bc, time, x[idM], y[idM], z[idM], nx, ny, nz, HxM, HyM, HzM, ExM, EyM, EzM, &HxP, &HyP, &HzP, &ExP, &EyP, &EzP);
	      }

	      s_dHx[es][n] = HxP-HxM;
	      s_dHy[es][n] = HyP-HyM;
	      s_dHz[es][n] = HzP-HzM;
	      s_dEx[es][n] = ExP-ExM;
	      s_dEy[es][n] = EyP-EyM;
	      s_dEz[es][n] = EzP-EzM;
	    }
	  }
	}
      }
    
      // interpolate to quadrature nodes
      for(int es=0;es<p_NblockS;++es;@inner(1)){
	for(int n=0;n<p_cubMaxNodes1;++n;@inner(0)){ // cubMaxNodes1 = max(intNfp,Np)
	  r_e = eo + es;
	  if(r_e<Nelements){
	    dfloat idHx = 0, idHy = 0, idHz = 0;
	    dfloat idEx = 0, idEy = 0, idEz = 0;
	    
	    for(int m=0;m<p_intNfp;++m){
	      const dfloat iInm = intInterp[ (n+face*p_intNfp) + m*p_Nfaces*p_intNfp];
	      idHx += iInm*s_dHx[es][m];
	      idHy += iInm*s_dHy[es][m];
	      idHz += iInm*s_dHz[es][m];	      
	      idEx += iInm*s_dEx[es][m];
	      idEy += iInm*s_dEy[es][m];
	      idEz += iInm*s_dEz[es][m];	      
	    }
	    
	    // load surface geofactors for this face
	    const dlong sid   = p_Nsgeo*(element*p_Nfaces+face);
	    const dfloat nx   = sgeo[sid+p_NXID];
	    const dfloat ny   = sgeo[sid+p_NYID];
	    const dfloat nz   = sgeo[sid+p_NZID];
	    const dfloat sJ   = sgeo[sid+p_SJID];
	    const dfloat invJ = sgeo[sid+p_IJID];

	    const dlong iid  = element*p_intNfp*p_Nfaces + n + face*p_intNfp;
	    const dfloat4 iweights = intUpwindCoeffs[iid];

	    // evaluate "flux" terms: (sJ/J)*(A*nx+B*ny)*(q^* - q^-)
	    const dfloat sc = p_half*invJ*sJ;
	    
            dfloat ifluxHx, ifluxHy, ifluxHz;
	    dfloat ifluxEx, ifluxEy, ifluxEz;
	    
            upwind(nx, ny, nz, iweights, idHx, idHy, idHz, idEx, idEy, idEz, &ifluxHx, &ifluxHy, &ifluxHz, &ifluxEx, &ifluxEy, &ifluxEz);
	    
            s_ifluxHx[es][n] = sc*ifluxHx;
            s_ifluxHy[es][n] = sc*ifluxHy;
            s_ifluxHz[es][n] = sc*ifluxHz;
            s_ifluxEx[es][n] = sc*ifluxEx;
            s_ifluxEy[es][n] = sc*ifluxEy;
            s_ifluxEz[es][n] = sc*ifluxEz;
          }
        }
      }

      // lift this face to element nodes
      for(int es=0;es<p_NblockS;++es;@inner(1)){
	for(int n=0;n<p_cubMaxNodes1;++n;@inner(0)){ // cubMaxNodes1 = max(intNfp,Np)
	  
	  if(n<p_Np){
	    for(int m=0;m<p_intNfp;++m){
	      const dfloat L = intLIFT[n+(m+face*p_intNfp)*p_Np];
	      LfluxHx += L*s_ifluxHx[es][m];
	      LfluxHy += L*s_ifluxHy[es][m];
	      LfluxHz += L*s_ifluxHz[es][m];
	      LfluxEx += L*s_ifluxEx[es][m];
	      LfluxEy += L*s_ifluxEy[es][m];
	      LfluxEz += L*s_ifluxEz[es][m];
	    }
	  }
	}
      }
    } // end face loop
    
    // for each node in the element
    for(int es=0;es<p_NblockS;++es;@inner(1)){
      for(int n=0;n<p_cubMaxNodes1;++n;@inner(0)){  // cubMaxNodes1 = max(intNfp,Np)
        if(r_e<Nelements){
          if(n<p_Np){

            const dlong base = element*p_Np*p_Nfields+n;
            rhsq[base+0*p_Np] += LfluxHx;
            rhsq[base+1*p_Np] += LfluxHy;
            rhsq[base+2*p_Np] += LfluxHz;
            rhsq[base+3*p_Np] += LfluxEx;
            rhsq[base+4*p_Np] += LfluxEy;
            rhsq[base+5*p_Np] += LfluxEz;
          }
        }
      }
    }
  }
}
